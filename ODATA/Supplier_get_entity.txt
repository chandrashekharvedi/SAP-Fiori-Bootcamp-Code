  METHOD supplierset_get_entity.
    "Step 1: Declare all the required data type
    DATA: lv_bp_id    TYPE bapi_epm_bp_id,
          lv_prod_id  TYPE bapi_epm_product_id,
          ls_product  TYPE bapi_epm_product_header,
          ls_bapi_bps TYPE bapi_epm_bp_header,
          lt_return   TYPE TABLE OF bapiret2.



    "Step 2: Reading the Key properties of the entity

    READ TABLE it_key_tab INTO DATA(ls_key) WITH KEY name = 'BP_ID'.

    IF sy-subrc = 0.

      lv_bp_id = ls_key-value.

    ELSE.

      READ TABLE it_key_tab INTO ls_key WITH KEY name = 'PRODUCT_ID'.

      IF sy-subrc = 0.
        lv_prod_id = ls_key-value.

        call function 'BAPI_EPM_PRODUCT_GET_DETAIL'
          EXPORTING
            product_id         =       lv_prod_id           " EPM: Product header data of BOR object SEPM002
          IMPORTING
            headerdata         =       ls_product           " EPM: Product header data of BOR object SEPM002
          .

        "Extract the supplier id from product data
        lv_bp_id = ls_product-supplier_id.

      ENDIF.

    ENDIF.

    IF lv_bp_id IS INITIAL.

      RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
        EXPORTING
          message_unlimited = |Hey! You haven't provided the ID of the Supplier|.


    ENDIF.

    "Step 3: Read the data from BAPI / FM / Select using the key
    CALL FUNCTION 'BAPI_EPM_BP_GET_DETAIL'
      EXPORTING
        bp_id      = lv_bp_id       " EPM: Business Partner ID to be used in BAPIs
      IMPORTING
        headerdata = ls_bapi_bps       " EPM: Business Partner header data ( BOR SEPM004 )
      TABLES
        return     = lt_return.          " Return Parameter

    IF lt_Return IS NOT INITIAL.

      me->mo_context->get_message_container( )->add_messages_from_bapi(
        EXPORTING
          it_bapi_messages          =       lt_return           " Return parameter table
      ).

      RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
        EXPORTING
          message_container = me->mo_context->get_message_container( ).

    ENDIF.


    "Step 4: Mapping the data in the output of er_entity

    MOVE-CORRESPONDING  ls_bapi_bps TO er_entity.
  ENDMETHOD.